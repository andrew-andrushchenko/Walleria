plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

// Load local properties with API keys
def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    compileSdk rootProject.compile_sdk_version

    defaultConfig {
        applicationId "com.andrii_a.walleria"
        minSdk rootProject.min_sdk_version
        targetSdk rootProject.target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField("String", "UNSPLASH_ACCESS_KEY", localProperties['unsplash_access_key'])
        buildConfigField("String", "UNSPLASH_SECRET_KEY", localProperties['unsplash_secret_key'])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        freeCompilerArgs += '-opt-in=kotlin.Experimental'
        freeCompilerArgs += "-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"

    // Compose libs
    implementation "androidx.compose.compiler:compiler:$compose_compiler_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"

    implementation "androidx.activity:activity-compose:$activity_compose_version"

    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_ktx_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$view_model_compose_version"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Navigation + compose
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"
    implementation "androidx.hilt:hilt-navigation:$hilt_navigation_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_compose_version"

    // Paging + compose
    implementation "androidx.paging:paging-runtime:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version_compose"

    // Retrofit + GSON
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"

    // Coil + compose
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    // DataStore
    implementation "androidx.datastore:datastore-preferences:$datastore_version"

    // Custom tabs
    implementation "androidx.browser:browser:$custom_tabs_version"

    // Android splash screen
    implementation "androidx.core:core-splashscreen:$splash_screen_version"

    // Accompainst libs
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompainst_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompainst_version"
    //implementation "com.google.accompanist:accompanist-navigation-animation:$accompainst_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompainst_version"
    implementation "com.google.accompanist:accompanist-pager:$accompainst_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompainst_version"

    // Tests
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$text_ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}